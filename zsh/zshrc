
export DOTFILES="$HOME/dotfiles"

#-------------------------------------------------
# VARIABLES
#-------------------------------------------------

# tabula rasa
umask 22
ulimit -c 0

# helper: removed later
append_path() {
  test -d "$1/sbin"      && path+="$1/sbin"
  test -d "$1/bin"       && path+="$1/bin"
  test -d "$1/share/man" && manpath+="$1/share/man"
}

# helper: removed later
prepend_path() {
  test -d "$1/sbin"      && path=("$1/sbin" $path)
  test -d "$1/bin"       && path=("$1/bin" $path)
  test -d "$1/share/man" && manpath=("$1/share/man" $manpath)
}

path=()
manpath=()

append_path "$DOTFILES"
append_path "$HOME/local"
append_path "/usr/local"
append_path "/usr"
append_path ""
append_path "/usr/X11"

export EDITOR="vim"
export PAGER="less"
export BLOCKSIZE="K"

export LANG="en_US.UTF-8"
export LC_ALL="en_US.UTF-8"

export RLWRAP_HOME="$HOME/.rlwrap"

if [ -n "$TMUX" ]; then
  export TERM="screen-256color"
else
  export TERM="xterm-256color"
fi

#-------------------------------------------------
# SHELL
#-------------------------------------------------

# behavior
setopt nobeep
setopt interactive_comments

# history-related
setopt hist_ignore_dups
setopt hist_find_no_dups
setopt hist_ignore_space
setopt extended_history
setopt append_history
HISTFILE=~/.zsh_history
HISTSIZE=1000
SAVEHIST=1000

# prompt
setopt prompt_subst

# completion
autoload -U compinit
compinit
setopt list_rows_first
setopt list_packed
LISTMAX=250
zstyle ':completion:*' menu select
zstyle ':completion:*' completer _expand_alias _complete _ignored

#-------------------------------------------------
# PROMPT
#-------------------------------------------------

PS1_COLORS=(7) # white

# for debugging
PS4='+%N:%i:%_>'

# exit code in red if != 0
# %m: hostname
# %F,%f, %K,%k: fg/bg color set/reset
PS1='%(?..%F{red}exit: $?
)%F{$PS1_COLORS[1]}%K{$PS1_COLORS[2]}%m%f%k '
RPS1='%F{236}%/%f' # gray-ish

#-------------------------------------------------
# ZLE
#-------------------------------------------------

# emacs
bindkey -e

# magic-space!
bindkey ' ' magic-space

# up/down history search
autoload -U history-search-end
zle -N history-beginning-search-forward-end history-search-end
zle -N history-beginning-search-backward-end history-search-end
bindkey '^[[A' history-beginning-search-backward-end
bindkey '^[[B' history-beginning-search-forward-end

# press Alt+. to insert the last word from the previous command
autoload -U smart-insert-last-word
zle -N smart-insert-last-word
bindkey "\e." smart-insert-last-word

# replacements
bindkey -s "\e\`" '$(!!)'
bindkey -s "\eh" "vim-\n"
bindkey -s "\ew" "warp\n"

# delete until slash
backward-kill-word-bash () {
  select-word-style bash
  zle backward-kill-word
  select-word-style shell # restore
}
zle -N backward-kill-word-bash
bindkey "^f" backward-kill-word-bash

autoload select-word-style
select-word-style shell

# enable ctrl-x-e to edit command line
autoload -U edit-command-line
zle -N edit-command-line
bindkey '^x^e' edit-command-line

#-------------------------------------------------
# COMMANDS
#-------------------------------------------------

for f in "$DOTFILES"/zsh/commands/*.zsh; do
  source $f
done

#-------------------------------------------------
# LOCAL
#-------------------------------------------------

[ -e "$DOTFILES"/zsh/local.zsh ] && source "$DOTFILES"/zsh/local.zsh

#-------------------------------------------------
# CLEANUP
#-------------------------------------------------

unset -f append_path
unset -f prepend_path

