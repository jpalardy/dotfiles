#!/usr/bin/env awk-f

function report_warning(msg) {
  warnings++
  printf "%s:%d:0: %s\n", (FILENAME == "-" ? "<stdin>" : FILENAME), FNR, msg
}

#-------------------------------------------------

BEGINFILE {
  empty_lines = 0
  deck_lines = 0
  delete questions
}

#-------------------------------------------------

# look for 2+ empty lines
{
  if (NF == 0) {
    empty_lines++
    if (deck_lines == 1) {
      report_warning("card doesn't have enough lines")
    }
    if (deck_lines > 4) {
      report_warning("card has too many lines")
    }
    deck_lines = 0
  } else {
    empty_lines = 0
    deck_lines++
  }
  if (empty_lines == 2) {
    report_warning("too many empty lines")
  }
}

deck_lines == 1 {
  if ($0 in questions) {
    report_warning("duplicate question from line " questions[$0])
    next
  }
  questions[$0] = FNR
}

deck_lines == 3 && $0 !~ /^[0-9][0-9][0-9][0-9]-[0-9][0-9]-[0-9][0-9]$/ {
  report_warning("invalid date format")
}

deck_lines == 4 && $0 !~ /^(([0-9]{4}-[0-9]{2}-[0-9]{2}|\?\?\?\?-\?\?-\?\?) [01])(, ([0-9]{4}-[0-9]{2}-[0-9]{2}|\?\?\?\?-\?\?-\?\?) [01])*$/ {
  report_warning("invalid history format")
}

#-------------------------------------------------

END {
  if (warnings > 0) {
    exit 1
  }
}

