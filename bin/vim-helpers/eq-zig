#!/usr/bin/env awk-f

function columns_as_printf() {
  printf("%sstd.debug.print(\"", leading_spaces)
  printf("%s = {}", $2)
  for (i=3; i<=NF; i++) {
    printf(", %s = {}", $i)
  }
  printf("\\n\", .{")
  printf("%s", $2)
  for (i=3; i<=NF; i++) {
    printf(", %s", $i)
  }
  printf("});")
}

{
  match($0, "^\\s*")
  leading_spaces = substr($0, 1, RLENGTH)
}

#
# lines like:
#   tap x y z
#   tap x, y, z
# are expanded to:
#   std.debug.print("x = {}, y = {}, z = {}\n", .{x, y, z});
#
$1 == "tap" && NF > 1 {
  # survive dangling `,`
  gsub(",", "")
  columns_as_printf()
  next
}

# passthrough
{
  print
}

