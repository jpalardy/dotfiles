#!/usr/bin/env awk-f

function report_warning(msg) {
  warnings++
  printf "%s:%d:0: %s\n", (FILENAME == "-" ? "<stdin>" : FILENAME), FNR, msg
}

#-------------------------------------------------

BEGINFILE {
  prev_date = ""
  prev_line = 0
  empty_lines = 0
}

#-------------------------------------------------

# look for badly sorted date headers
/^20[0-9][0-9]-[0-9][0-9]-[0-9][0-9]$/ {
  date = $0
  if (date == prev_date) {
    report_warning("not chronological: repeated from line " prev_line)
  } else if (date < prev_date) {
    report_warning("not chronological: after " prev_date " from line " prev_line)
  }
  prev_date = date
  prev_line = FNR
}

# look for bad dates -- bad number of days in month, bad number of months in year...
match($0, /\<([0-9][0-9][0-9][0-9]-[0-9][0-9]-[0-9][0-9])\>/, arr) {
  date = mktime(gensub("-", " ", "g", arr[1]) " 0 0 0")
  if (date == -1) {
    report_warning("invalid date: " arr[1])
    next
  }
  if (strftime("%F", date) != arr[1]) {
    report_warning("invalid date: " arr[1])
  }
}

# look for 2+ empty lines
{
  if (NF == 0) {
    empty_lines++
  } else {
    empty_lines = 0
  }
  if (empty_lines == 2) {
    report_warning("too many empty lines")
  }
}

#-------------------------------------------------

END {
  if (warnings > 0) {
    exit 1
  }
}

