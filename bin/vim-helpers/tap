#!/usr/bin/env awk-f

# -------------------------------------------------

#
# lines like:
#   tap x y z
#   tap x, y, z
# become:
#   [x: x, y: y, z: z] |> IO.inspect()
#
function format_elixir() {
  printf("[")
  for (i=2; i<=NF; i++) {
    if (i>2) printf(", ")
    printf("%s: %s", $i, $i)
  }
  print("] |> IO.inspect()")
}

#
# lines like:
#   tap x y z
#   tap x, y, z
# become:
#   print(vim.inspect({x = x, y = y, z = z}))
#
function format_lua() {
  printf("print(vim.inspect({ ")
  for (i=2; i<=NF; i++) {
    if (i>2) printf(", ")
    printf("%s = %s", $i, $i)
  }
  print(" }))")
}

#
# lines like:
#   tap x y z
#   tap x, y, z
# become:
#   std.debug.print("x = {}, y = {}, z = {}\n", .{x, y, z});
#
function format_zig() {
  printf("std.debug.print(\"")
  for (i=2; i<=NF; i++) {
    if (i>2) printf(", ")
    printf("%s = {}", $i)
  }
  printf("\\n\", .{")
  for (i=2; i<=NF; i++) {
    if (i>2) printf(", ")
    printf("%s", $i)
  }
  print("});")
}

#
# lines like:
#   tap x y z
#   tap x, y, z
# become:
#   let _ = Debug.log "" { x = x, y = y, z = z } in
#
function format_elm() {
  printf("let _ = Debug.log \"\" { ")
  for (i=2; i<=NF; i++) {
    if (i>2) printf(", ")
    printf("%s = %s", $i, $i)
  }
  print(" } in")
}

# -------------------------------------------------

{
  match($0, "^\\s*")
  leading_spaces = substr($0, 1, RLENGTH)
}

$1 == "tap" && NF > 1 {
  # survive dangling `,`
  gsub(",", "")
  printf("%s", leading_spaces)
  fname = "format_" filetype
  @fname()
  next
}

# passthrough
{
  print
}

