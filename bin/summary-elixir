#!/bin/bash

awk -v "current_line=$CURRENT_LINE" '

BEGIN {
  CLEAR_ATTRS  = "\033[0m"
  BOLD         = "\033[1m"
  DIM          = "\033[2m"
  COLOR_RED    = "\033[31m"
  COLOR_GREEN  = "\033[32m"
  COLOR_YELLOW = "\033[33m"
  COLOR_BLUE   = "\033[34m"

  highlights["def"] = COLOR_GREEN
  highlights["defp"] = DIM
  highlights["defmodule"] = COLOR_BLUE
  highlights["defdelegate"] = BOLD COLOR_GREEN
  highlights["defmacro"] = COLOR_RED
  highlights["defimpl"] = COLOR_YELLOW
  highlights["@callback"] = COLOR_YELLOW
  highlights["@behaviour"] = COLOR_YELLOW

  highlights["describe"] = BOLD
  highlights["setup"] = COLOR_YELLOW
  highlights["test"] = ""
}

$1 == "@impl" {
  impl = 1
}

$1 in highlights {
  # remove details
  sub("^ *", "")
  sub("\\(.*", "")
  sub(", do:.*", "")
  sub(" do$", "")

  if (impl) { printf BOLD }
  print highlights[$1] $0 CLEAR_ATTRS

  impl = 0
}

NR == current_line {
  print "[...]"
}

' "$@" | uniq -c | awk '

$1 == 1 { sub("1", " ") }
{ print }

'
